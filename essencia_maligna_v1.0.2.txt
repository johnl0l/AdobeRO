//===== Script ====================================
//= Essências Malignas                            
//===== Autor =====================================
//= O Diabo                                       
//===== Versão ====================================
//= v1.0.2 Criação do Script                       
//===== Descrição =================================
//= Sistema de pontuação: Da pontos ao jogador    
//= que matar monstros, com recompensas maiores  
//= para monstros do tipo Chefe. Possui loja de 
//= pontos.            
//===== Mudanças ==================================
//= Nenhuma                                       
//================================================= 

-	shop	shop	-1,1750:-1

prontera, 160, 150, 4	script	Estudante de Essências#1	4_M_KHKIEL,{

	setarray @equipamentos[0], 2302, 100;				// ID do Item, Preço - Ex: Camisa de Algodão[1] custando 100.
	setarray @consumiveis[0], 512, 10;				// ID do Item, Preço - Ex: Maçã custando 10.

	set @npcname$, "[ ^cf7500Estudante de Essências^000000 ]";	// Nome do NPC	

									// Está parte realiza a limpeza da loja para evitar itens duplicados.
	
	for(set .@i, 0; .@i < getarraysize(@consumiveis); set .@i, .@i+2)
		npcshopdelitem "shop", @consumiveis[.@i];
	for(set .@i, 0; .@i < getarraysize(@equipamentos); set .@i, .@i+2)
		npcshopdelitem "shop", @equipamentos[.@i];
	npcshopdelitem "shop", 1750;

        mes @npcname$;
        mes "Olá ^0000FF"+strcharinfo (0)+"^000000, eu tenho pesquisado a bastante tempo o comportamento estranho das ^8000FFEssências Malignas^000000 e posso te recompensar caso me traga algumas para que eu continue estudando o comportamento delas.";
        next;
        mes @npcname$;
        mes "Eu posso trocar as ^8000FFEssências Malignas^000000 por algumas recompensas.";
	next;
	mes @npcname$;
        mes "Vejo que você tem ^8000FF"+#essencias_malignas+"^000000 delas.";
        next;
        switch(select("- Consumíveis","- Equipamentos","- Essências Malignas ?")) {
                case 1:
                        set @tipo, 1;
                        for(set .@i, 0; .@i < getarraysize(@consumiveis); set .@i, .@i+2) {
                                npcshopadditem "shop", @consumiveis[.@i], @consumiveis[.@i+1];
			}
                        callshop "shop", 1;
                        npcshopattach "shop";
                        break;
                case 2:
                        set @tipo, 2;
                        for(set .@i, 0; .@i < getarraysize(@equipamentos); set .@i, .@i+2){
                                npcshopadditem "shop", @equipamentos[.@i], @equipamentos[.@i+1];
			}
                        callshop "shop",1;
                        npcshopattach "shop";
                        break;
		case 3:
			mes @npcname$;
			mes "As ^8000FFEssências Malignas^000000 são deixadas por monstros do tipo ^FF0000Chefe^000000, mas em números irregulares. Ela parece guardar algumas das características do Chefe morto.";
			next;
			mes @npcname$;
			mes "Ainda estou estudando seus comportamentos e por isso estou atrás delas.";
        		close;
	}
	end;
									// Calcula o valor total da compra

        OnBuyItem:
           	set @total, 0;
                for(set @i, 0; @i < getarraysize(@bought_nameid); set @i, @i+1) {

									// Tipo da compra (Consumível ou Equipamento)
                        
			switch(@tipo) {
                                case 1:
                                        for(set @j, 0; @j < getarraysize(@consumiveis[0]); set @j, @j+2)
                                                if(@consumiveis[@j] == @bought_nameid[@i])
                                                        set @total, @total + (@consumiveis[@j+1] * @bought_quantity[@i]);
					for(set .@i, 0; .@i < getarraysize(@consumiveis); set .@i, .@i+2)
						npcshopdelitem "shop", @consumiveis[.@i];
                                        break;
                                case 2:
                                        for(set @j, 0; @j < getarraysize(@equipamentos[0]); set @j, @j+2)
                                                if(@equipamentos[@j] == @bought_nameid[@i])
                                                        set @total, @total + (@equipamentos[@j+1] * @bought_quantity[@i]);
					for(set .@i, 0; .@i < getarraysize(@equipamentos); set .@i, .@i+2)
						npcshopdelitem "shop", @equipamentos[.@i];
                                        break;
                        }
                }
                if(#essencias_malignas >= @total) {
				
									// Validação da compra
			set @peso_compra, 0;
			getinventorylist;
			if(@inventorylist_count +  getarraysize(@bought_nameid) > 100){
				mes @npcname$;
				mes "Você tem muitos itens no seu inventário. Tente guardar alguns.";
				close;
			}
			for(set @i, 0; @i < getarraysize(@bought_nameid); set @i, @i+1)
				set @peso_compra, @peso_compra + getiteminfo(@bought_nameid[@i], 6) * @bought_quantity[@i];
			if(@peso_compra >= maxweight - weight){
				mes @npcname$;
				mes "Vá com calma ! Você não consegue carregar tudo isso.";
				close;
			}
				
                        set #essencias_malignas, #essencias_malignas - @total;
                        for(set @i, 0; @i < getarraysize(@bought_nameid); set @i, @i+1)
                                getitem @bought_nameid[@i],@bought_quantity[@i];
                        dispbottom "Você gastou "+@total+" Essências Malignas, lhe restam "+#essencias_malignas+" Essências Malignas.";
                } else {
			mes @npcname$;
			mes "Desculpe, mas você não tem ^8000FFEssências Malignas^000000 suficientes para está compra.";
			close;
		}
}

-	script	drop	-1,{
OnNPCKillEvent:

	set .@comum_drop, 10;						// Chance de obter essência de um monstro comum. Monstros comuns deixam apenas 1 essência por vez
									// 0  = Monstros comuns não dropam essência
									// 1  = 1%
									// 10 = 10%

	set .@dividir, 1;						// Essências para grupos
									// 0 = Essência para para grupos desativada.
									// 1 = Essência para para grupos Ativada. (As essências não são dividas, todos do grupo que estiverem no mapa ganham uma quantiade entre @min_drop e @max_drop caso)

	set .@min_drop, 10;						// Quantidade mínima possível a se adquirir. - Ex: Quantidade aleátoria de 10 a @max_drop
	set .@max_drop, 50;						// Quantidade máxima possível a se adquirir. - Ex: Quantidade aleátoria de .@min_drop a 50
								
									//ID dos Chefes que droparão Essências Malignas

	setarray .@chefes[0], 1312, 1251, 1159, 2022, 1492, 1038, 1086, 1511, 1630, 1688, 1252, 1583, 1751, 1768, 1734, 1871, 1120, 1582, 1096, 1147, 1647, 1651, 1644, 1642, 1646, 1643, 1916, 1785, 1112, 1373, 1046, 1059, 1650, 1039, 1874, 2068, 1719, 1389, 1115, 1658, 1157, 1150, 1885, 1832, 1779, 1980, 1990, 1708, 1648, 1917, 1087, 1623, 1272, 1190, 1418, 1649, 1685, 2443, 2476, 2113, 3091, 2319, 2996, 2238, 2240, 2475, 2236, 2253, 3097, 2529, 2251, 2165, 3073, 2241, 2255, 2442, 2202, 2156, 2131, 3000, 3092, 2362, 2235, 2237, 2249, 2087, 3029, 3096, 2239, 2441, 3074;
	for(set @a, 0; @a <= getarraysize(.@chefes); set @a, @a+1)
		if(killedrid == .@chefes[@a]) {
			if(.@dividir == 1 && getcharid(1)){
				getpartymember(getcharid(1), 2);
				getpartymember(getcharid(1), 1);
				.@map_killer$ = strcharinfo(3);
				for(.@i = 0; .@i < $@partymembercount; .@i++)
					if(isloggedin($@partymemberaid[.@i], $@partymembercid[.@i]))
						if(!getmapxy(.@map$, .@x, .@y, 0, rid2name($@partymemberaid[.@i])) && .@map$ == .@map_killer$) {
							attachrid($@partymemberaid[.@i]);
							set @essencia_ganha, rand(.@min_drop,.@max_drop);
							dispbottom "Você coletou "+@essencia_ganha+" Essências Malignas";
							set #essencias_malignas, #essencias_malignas + @essencia_ganha;
						}
				end;
			} else {
				set @essencia_ganha, rand(.@min_drop,.@max_drop);
				dispbottom "Você coletou "+@essencia_ganha+" Essências Malignas";
				set #essencias_malignas,#essencias_malignas+ @essencia_ganha;
			}	
		}
	if(.@dividir == 1 && getcharid(1)){
		getpartymember(getcharid(1), 2);
		getpartymember(getcharid(1), 1);
		.@map_killer$ = strcharinfo(3);
		for(.@i = 0; .@i < $@partymembercount; .@i++)
			if(isloggedin($@partymemberaid[.@i], $@partymembercid[.@i]))
				if(!getmapxy(.@map$, .@x, .@y, 0, rid2name($@partymemberaid[.@i])) && .@map$ == .@map_killer$) {
					attachrid($@partymemberaid[.@i]);
					set @chance, rand(1, 100);
					if(@chance <= .@comum_drop)
						set #essencias_malignas, #essencias_malignas + 1;
				}
		end;
	} else {
		set @chance, rand(1, 100);
		if(@chance <= .@comum_drop)
			set #essencias_malignas, #essencias_malignas + 1;
	}
}