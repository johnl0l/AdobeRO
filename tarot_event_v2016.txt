/*
				.___  ___.  __   __  ___ 
				|   \/   | |  | |  |/  / 
				|  \  /  | |  | |  '  /  
				|  |\/|  | |  | |    <   
				|  |  |  | |  | |  .  \  
				|__|  |__| |__| |__|\__\ 
                         
	= Tarot Event v2016
	= by Mikéias
	= brAthena
	= User: http://forum.brathena.org/index.php/user/11630-mikeias/

*/

prontera,157,175,3	script	[Tarot Event]	4_M_BARD,{

	mes .namenpc$;
	mes "Olá "+strcharinfo(0);
	mes "Está afim de testar sua sorte nas cartas? Fique atento quando anunciar o início do evento.";
	next;
	switch(select("Participar do evento", ( getgroupid() >= $@lvminGM ? ( !$tarot_status ? "Ligar":"Desligar")+" Evento":"") , "Premiação", "Não quero participar")){
	
		case 1:
			// = Se evento não ativo
			if (!$tarot_status){
				mes .namenpc$;
				mes "Senhor"+(Sex? "":"a")+", no momento o evento não está atívo! Você só poderá se registrar na fila de espera quando anunciar.";
				close;
			}
			
			// = Verifica se o nome do personagem já está no array
			for (set .@i,0; .@i < getarraysize($@tarot_participantes$); set .@i,.@i + 1) {
				if ( $@tarot_participantes$[.@i] == strcharinfo(0) ) {
					message strcharinfo(0), strnpcinfo(1)+" Você já está registrado. Aguarde!";
					close;
				}
			}
		
			// = Adiciona o nome do personagem ao array
			set $@tarot_participantes$[getarraysize($@tarot_participantes$)], strcharinfo(0);
			message strcharinfo(0), strnpcinfo(1)+" Meus parabéns! Você se registrou com êxito para participar do evento.";
			announce strnpcinfo(1)+" <"+strcharinfo(0)+"> registrou-se para participar do evento!",bc_all;
			close;
		
		case 2:
			if( agitcheck() || agitcheck2() ){
				mes .namenpc$;
				mes "Neste momento a Guerra do Emperium está atíva e não é possível dar início ao evento!";
				close;
			}
			mes .namenpc$;
			mes "Olá, Game-Master! Você tem certeza que deseja "+( !$tarot_status ? "Ligar":"Desligar")+" o evento?";
			if(select("Não:Tenho certeza") == 1) close;
			if ( !$tarot_status ){
				donpcevent "Tarot Event::OnStart";
			}else
				donpcevent "Tarot Event::OnStop";
			close;
			end;
			
		case 3:
			mes .namenpc$;
			mes "O participante que não for atingido pela carta 'Coma' receberá:";
			mes " ";
			for(set .@w, 0; .@w < getarraysize($@premiacaoITEM); set .@w, .@w + 1){
				set .@item, $@premiacaoITEM[.@w];
				set .@quantidade, $@premiacaoQUANT[.@w];
				mes .@quantidade+"x "+getitemname(.@item);
			}
			close;
		
		case 4:
		close;
	
	}
	
	OnInit:
	// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
	set .namenpc$, "^800000"+strnpcinfo(1)+"^000000";
	// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
	setarray $@mapeventtarot$, "poring_w01", 102, 71;	// = Configuração do mapa do evento
	// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
	set $@mintarot_participantes,2;	// = Número mínimo de participantes para iniciar o evento
	// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
	set $@lvminGM, 60;	// = Level mínimo de GM para ligar/desligar o evento
	// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
	// = ATENÇÃO: CADA ITEM TEM SUA QUANTIDADE
	setarray $@premiacaoITEM, 607, 608, 609;	// = Itens
	setarray $@premiacaoQUANT, 1, 10, 20;	// = Quantidade de cada item
	// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
	
	callfunc ("resetEventoTarot", $@tarot_participantes$, $@mapeventtarot$[0]);
	setarray .mapflag,0,1,7,8,13,22,33,34,12;
	for (set .@m,0; .@m < getarraysize(.mapflag); set .@m, .@m + 1){
		setmapflag $@mapeventtarot$[0], .mapflag[.@m];
	}
	end;

}

-	script	Tarot Event	-1,{

	// = Início automático
	OnClock1100:
	OnClock1300:
	OnClock1700:
	OnClock1900:
	
	// = Início manual
	OnStart:
	set $tarot_status,1;
	for(set .@t, 3; .@t > 0; set .@t, .@t - 1){
	
		// = Se variável 0 ou GDE ativa, então encerra.
		if ( !$tarot_status || agitcheck() || agitcheck2() ){
			callfunc ("resetEventoTarot", $@tarot_participantes$, $@mapeventtarot$[0]);
			end;
		}
		
		announce "["+strnpcinfo(1)+"] O evento iniciará em "+(.@t*20)+" segundos!",bc_all;
		sleep 20000;
	}
	
	if ( getarraysize($@tarot_participantes$) < $@mintarot_participantes ){
		announce "["+strnpcinfo(1)+"] O mínimo de participantes requeridos não foi satisfeito!",bc_all;
		callfunc ("resetEventoTarot", $@tarot_participantes$, $@mapeventtarot$[0]);
		end;
	}
	announce "["+strnpcinfo(1)+"] Começando!",bc_all;
	
	set $tarot_status,0;
	for(set .@p, 0; .@p < getarraysize($@tarot_participantes$); set .@p, .@p + 1){
	
		if ( isloggedin(getcharid(3, $@tarot_participantes$[.@p])) ){
			warpchar $@mapeventtarot$[0], atoi($@mapeventtarot$[1]), atoi($@mapeventtarot$[2]), getcharid(0, $@tarot_participantes$[.@p]);
		}else{
			deletearray $@tarot_participantes$[.@p], 1;
		}
		
	}
	sleep 8000;
	donpcevent "[GM] Cartomante::OnSpeak";
	end;
	
	OnReturn:
	while( getarraysize($@tarot_participantes$) > 1){ // = Enquanto o total de participantes for maior que 1
	
		sleep 2000;
		for(set .@p,0;.@p<getarraysize($@tarot_participantes$);set .@p,.@p+1){
			attachrid(getcharid(3,$@tarot_participantes$[.@p]));
				if( strcharinfo(3) == $@mapeventtarot$[0] ){
					set .@CARDs,522+rand(1,14);
					specialeffect2 .@CARDs, strcharinfo(0);
					if( .@CARDs == 531 ){
						deletearray $@tarot_participantes$[.@p], 1;
						percentheal -99,-99;
						sleep2 1500;
						set @randtarot, rand(1,3);
						set #CASHPOINTS, #CASHPOINTS+@randtarot;
						dispbottom "Você recebeu ["+@randtarot+"] CASHPOINTS por participar do evento (Tarot).";
						warp "SavePoint",0,0;
					}
				}
				detachrid;
		}
	}
	sleep 2000;

	if ( getarraysize($@tarot_participantes$)){
		attachrid(getcharid(3, $@tarot_participantes$[0]));	// = Atacha o account_id apartir do nome
		if ( strcharinfo(3) == $@mapeventtarot$[0] ){
			announce "["+strnpcinfo(1)+"] Temos um vencedor: ["+$@tarot_participantes$[0]+"]",bc_all;
			for(set .@w, 0; .@w < getarraysize($@premiacaoITEM); set .@w, .@w + 1){
				set .@item, $@premiacaoITEM[.@w];
				set .@quantidade, $@premiacaoQUANT[.@w];
				getitem .@item, .@quantidade;
				message strcharinfo(0), "Você recebeu: "+.@quantidade+"x "+getitemname(.@item);
			}
		}
		detachrid;
	}
	
	callfunc ("resetEventoTarot", $@tarot_participantes$, $@mapeventtarot$[0]);
	end;
	
	// = Encerra o Evento
	OnStop:
	announce "["+strnpcinfo(1)+"] Evento encerrado!",bc_all;
	callfunc ("resetEventoTarot", $@tarot_participantes$, $@mapeventtarot$[0]);
	end;

}

-	script	tarot_events	-1,{

	// = Se o participante que deslogou ou morreu estiver no mapa do evento, seu nome será retirado do array de tarot_participantes
	OnPCDieEvent:
	OnPCLogoutEvent:
	if ( strcharinfo(3) == $@mapeventtarot$[0] ){
		for (set .@i,0; .@i < getarraysize($@tarot_participantes$); set .@i,.@i + 1) {
		
			if ( $@tarot_participantes$[.@i] == strcharinfo(0) ){
				deletearray $@tarot_participantes$[.@i], 1;
				mapannounce $@mapeventtarot$[0], "[Tarot Event] ["+strcharinfo(0)+"] está fora do evento!",bc_map;
				warp getsavepoint(0), getsavepoint(1), getsavepoint(2);
				sleep2 1500;
				atcommand "@alive";
				break;
			}
			
		}
	}
	end;
}

poring_w01,102,73,1	script	[GM] Cartomante	2001,{
	end;
	
	OnSpeak:
	setarray .msgs$,
		"Usarei a habilidade 'Destino nas Cartas' em cada um!",
		"O vencedor será o último a sobreviver!",
		"Boa sorte a todos os participantes!",
		"TAROOOOOOW!";

	for ( set .@i,0; .@i < getarraysize(.msgs$); set .@i, .@i+1 ){
		npctalk .msgs$[.@i];
		sleep 3000;
	}
	donpcevent "Tarot Event::OnReturn";
	end;
	
	OnInit:
	setarray .x[0],94,105,99,110;
	setarray .y[0],68;
	npcspeed 195;
	initnpctimer;
	end;
	
	OnTimer1000:
	emotion rand(34,45);
	for(set .c,0;.c<getarraysize(.x); set .c,.c+1){
		npcwalkto .x[.c],.y[0];
		sleep 3500;
	}
	initnpctimer;
	end;
	
}

// = Reseta parâmetros
function	script	resetEventoTarot	{

	// @param0 = array dos tarot_participantes
	// @param1 = map event
	deletearray getarg(0);
	set $tarot_status,0;
	mapwarp getarg(1), "prontera",150,150;
	return;
}