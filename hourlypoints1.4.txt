//===== Hourly Points Script =========================================
//===== By: ==========================================================
//= GorthexTiger modified by Nibi
//===== Current Version: =============================================
//= 1.4
//===== Compatible With: =============================================
//= Any eAthena Version
//===== Description: =================================================
//= Get Points every successful hours of gameplay, you cannot get
//= the points even if you miss a second or a minute. A player will
//= get a very big bonus if they played 3 hours consecutively
//= or without logging out of the game. If the player is vending
//= the script will then stop.
//===== Additional Comments: =========================================
//= You can modify the script to your liking.
//= The default points is Kafrapoints change it anyway if you like.
//= 1.1 = Check Chatting too
//= 1.2 = 5 Minute Idle Check & @at/@autotrade check.
//= 1.3 = Corrected the current balance line on 12 Hours Consecutive
//= 1.4 = Added command to check remaining time and updated idle check. (Skorm)
//====================================================================
-	script	hourlypoints	-1,{
    
OnPointGet:
	while(checkvending() >= 1 || checkchatting() == 1 || checkidle() >= .idle) {
		if( .@mes$ == "" ) {
			dispbottom set( .@mes$, "The hourly points event haulted because you were vending, chatting, or idle." );
			set @hourly_points_timer, 0;
		}
		sleep2 .delay;
	}
	set #CASHPOINTS, #CASHPOINTS + .point_amt;
	dispbottom "You received "+.point_amt+" Kafrapoints by staying ingame for 1 hour";
	dispbottom "Current Balance = "+#CASHPOINTS+" Kafrapoints";
	set @consecutive_hour, @consecutive_hour + 1;

	//Check for 3 hours consecutive
	if(@consecutive_hour == 3) {
		set @consecutive_hour,0;
		set #CASHPOINTS, #CASHPOINTS + .cpoint_amt;
		dispbottom "You receive "+.cpoint_amt+" Kafrapoints in playing for 12 consecutive hours";
		dispbottom "Current Balance = "+#CASHPOINTS+" Kafrapoints";
	}
	
OnPCLoginEvent:
	addtimer .timer,"hourlypoints::OnPointGet";
	set @hourly_points_timer, gettimetick(2) + ( .timer / 1000 );
	end;
	
OnCmdHour:
	message strcharinfo(0),@hourly_points_timer ? Time2Str( @hourly_points_timer )+" remaining before you get your Hourly Reward(s).":"Something went wrong or you're considered idle at the moment try relogging!";
	end;

OnInit:
	bindatcmd "ctr","hourlypoints::OnCmdHour";
	set .timer, 1000*15*15; //Timer in milliseconds.
	set .cpoint_amt, 15; //Points gained for consecutive time online.
	set .point_amt, 5; //Normal points gained.
	set .delay, 1000; //Delay for idle re-check check.
	set .idle, 60; //Player is idle after not moving for this many seconds.
}